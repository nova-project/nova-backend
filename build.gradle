plugins {
  id 'base'
  id 'io.freefair.lombok' version '5.3.0' // apply, because javadoc
  id 'nebula.project' version '8.0.0' apply false
  id 'io.freefair.git-version' version '5.3.0'
  id 'com.github.spotbugs' version '4.6.0' apply false
//  id 'io.freefair.maven-optional' version '5.3.0' // apply, because javadoc

  id 'io.freefair.javadoc-links' version '5.3.0'
  id 'io.spring.javadoc' version '0.0.1' apply false
  id 'io.spring.javadoc-aggregate' version '0.0.1'
}

if (version.toString().endsWith('SNAPSHOT')) {
  version = 'edge'
} else if (version.toString().startsWith("v")) {
  version = version.toString().substring(1)
}

allprojects {
  group 'net.getnova.framework'

  ext {
    springBootVersion = '2.4.3'
    jakartaPersistanceApiVersion = '2.2.3'
    junitVersion = '5.7.0'
    mockitoVersion = '3.7.0'
  }

  repositories {
    mavenCentral()
  }
}

subprojects {
  version rootProject.version

  apply plugin: "nebula.project"
  apply from: "${rootDir}/gradle/java.gradle"
  apply from: "${rootDir}/gradle/lombok.gradle"
  apply from: "${rootDir}/gradle/test.gradle"
  apply from: "${rootDir}/gradle/checkstyle.gradle"
  apply from: "${rootDir}/gradle/spotbugs.gradle"
  apply from: "${rootDir}/gradle/pmd.gradle"
  apply from: "${rootDir}/gradle/javadoc.gradle"
  apply from: "${rootDir}/gradle/publish.gradle"
}
